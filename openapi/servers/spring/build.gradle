/*
 * Copyright (c) 2019 Connexta, LLC
 *
 * Released under the GNU Lesser General Public License version 3; see
 * https://www.gnu.org/licenses/lgpl-3.0.html
 */
/* Build Script */

plugins {
    id "org.openapi.generator"  version "${openApiVersion}"
}

configurations {
    openApiSpec
}

dependencies {
    openApiSpec project(":security-markings-api-openapi:security-markings-api-openapi-specs")
    compile project(":security-markings-api-openapi:security-markings-api-openapi-models")
    compile "javax.annotation:javax.annotation-api:${javaxAnnotationVersion}"
    compile "javax.validation:validation-api:${javaxValidationVersion}"
    compile "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"
    compile "io.springfox:springfox-swagger2:${swaggerVersion}"
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
}

// copy over the openapi spec
task copyOpenApiSpec(type: Copy) {
    // need the jar from the specs project in order to grab the openapi spec
    dependsOn ":security-markings-api-openapi:security-markings-api-openapi-specs:assemble"

    from zipTree(configurations.openApiSpec.singleFile).matching {
        include "openapi/openapi.yaml"
    }
    into "$buildDir"
}

// generate the openapi spring server
openApiGenerate {
    inputSpec = "${openApiDir}/openapi.yaml"
    outputDir = "${openApiGeneratedDir}"
    generatorName = "spring"
    validateSpec = false // openapi spec validation happens in the specs projects
    configOptions = [
        java11: "true",
        dateLibrary: "java11",
        useTags: "true",
        interfaceOnly: "true",
        useOptional: "true",
        booleanGetterPrefix: "is",
        useBeanValidation: "true",
        sortParamsByRequiredFlag: "true",
        performBeanValidation: "false",
        sourceFolder: "/src/main/java"
    ]
    systemProperties = [
            apis: "",
            apiDocs: "false",
            models: "false",
            modelDocs: "false",
            supportingFiles: "ApiUtil.java"
    ]
    apiPackage = "${openapiPackage}.servers.spring"
    modelPackage = "${openapiModelsPackage}"
}
// need the openapi spec in order to generate the openapi spring server
tasks.openApiGenerate.dependsOn tasks.copyOpenApiSpec

sourceSets {
    main {
        java {
            srcDirs = ["${openApiGeneratedDir}/src"]
        }
    }
}
// need the openapi models in order to add them to the source set
compileJava.dependsOn tasks.openApiGenerate
