/*
 * Copyright (c) 2019 Connexta, LLC
 *
 * Released under the GNU Lesser General Public License version 3; see
 * https://www.gnu.org/licenses/lgpl-3.0.html
 */
/* Build Script */
import org.gradle.internal.jvm.Jvm

buildscript {
    // define properties
    apply from: "${rootDir}/properties.gradle"
}

// plugins to be used by the root project
plugins {
    id "com.diffplug.gradle.spotless" version "${spotlessVersion}"
    id "org.owasp.dependencycheck" version "${owaspVersion}"
    id "org.sonarqube" version "${sonarqubeVersion}"
}

// java version check
if (javaVersion != Jvm.current().javaVersion.majorVersion) {
    throw new Exception("You need Java ${javaVersion} to build and run ${project.name}.\n" +
    "The current version installed is ${Jvm.current().javaVersion.majorVersion}.\n" +
    "For further reading see: \n\t> " +
    "https://github.com/connexta/ion-security-markings#prerequisites")
}

// actions to be performed on all projects
allprojects {
    buildscript {
        // define the plugin repository
        repositories {
            maven { url "https://plugins.gradle.org/m2/" }
        }

        // define properties
        apply from: "${rootDir}/properties.gradle"
    }

    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "maven-publish"

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    // specify groupId and version
    // artifactId is defaulted to the projects/subprojects name, i.e. security-markings-api-openapi-specs
    group = projectGroupId
    version = projectVersion

    // maven repositories
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://artifacts.codice.org/content/repositories/ion-releases/" }
    }

    // maven publishing plugin configuration
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version

                from(components.java)
            }
        }
    }
    assemble.finalizedBy tasks.publishToMavenLocal

    // dependency resolution
    configurations.all {
        resolutionStrategy {
            force "com.fasterxml.jackson.core:jackson-databind:[${jacksonDatabindVersion},)"
            force "com.google.guava:guava:[${guavaVersion},)"
            force "org.springframework:spring-context:[${springVersion},)"
            force "org.springframework:spring-aop:[${springVersion},)"
            force "org.springframework:spring-beans:[${springVersion},)"
            force "org.springframework:spring-expression:[${springVersion},)"
            force "org.springframework:spring-core:[${springVersion},)"
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    // other tasks

    task listDependencies(type: DependencyReportTask)
}

// tasks to be performed by the root project

// spotless plugin configuration
spotless {
    File javaLicense = rootProject.file("license-java.txt")
    File poundLicense = rootProject.file("license-pound.txt")

    format "markdown", SpotlessConfig.getMarkdown(poundLicense)
    format "yaml", SpotlessConfig.getYaml(poundLicense)
    java SpotlessConfig.getJava(javaLicense)
    groovyGradle SpotlessConfig.getGroovy(javaLicense)
}
compileJava.dependsOn tasks.spotlessCheck

// owasp plugin configuration
dependencyCheck {
    failBuildOnCVSS = 4
    failOnError = true

    analyzers {
        ossIndexEnabled = false
    }

    // add support for NVD mirror
    if (project.hasProperty("dependencyCheckUrlModified") && project.hasProperty("dependencyCheckUrlBase")) {
        println "Using NVD Mirrors: ${dependencyCheckUrlBase} and ${dependencyCheckUrlModified}"
        cve {
            urlModified = "${dependencyCheckUrlModified}"
            urlBase = "${dependencyCheckUrlBase}"
        }
    }

    suppressionFile = "${rootDir}/owasp-suppressions.xml"
}
